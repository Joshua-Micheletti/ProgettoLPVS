-- Progetto Linguaggi di Programmazione e Verifica del Software
-- Studente: Joshua Micheletti
-- Matricola: 283057

-- SCHEMA ARCHITETTURA

--          PUBLISHER                         BROKER                         SUBSCRIBER
-- + ------------------------- +      + ------------------- +      + ------------------------- +
-- |  + ----------- +          |      |    + --------- +    |      |          + ----------- +  |
-- |  | application |          |   +--|--> | connector | <--|--+   |          | application |  |
-- |  + ----------- +          |   |  |    + --------- +    |  |   |          + ----------- +  |
-- |       |                   |   |  |          |          |  |   |                   ^       |
-- |       |    + --------- +  |   |  |    + --------- +    |  |   |  + --------- +    |       |
-- |       +--> | connector | -|---+--|--> |   topic   |    |  +---|- | connector | ---+       |
-- |            + --------- +  |      |    + --------- + <--|------|> + --------- +            |
-- + ------------------------- +      + ------------------- +      + ------------------------- +


-- FUNZIONAMENTO ARCHITETTURA

-- Architettura pub-sub per intercomunicazione tra blockchain tramite middleman (broker).
-- La struttura consiste nella iscrizione di blockchain ad un broker tramite connettori.
-- Ogni blockchain si può iscrivere come publisher, subscriber o entrambi.
-- Le blockchain publisher possono creare un topic nel broker per rendere disponibili le proprie informazioni ad altre blockchain.
-- I publisher aggiornano periodicamente i topic quando ottengono nuove informazioni pertinenti
-- I subscriber si possono iscrivere a topic esistenti nel broker per ricevere le informazioni al loro interno.
-- Ogni volta che un topic viene aggiornato, le nuove informazioni vengono inviate a tutte le blockchain subscriber iscritte a quel topic.



-- SCHEMA ARCHITETTURA DELLA SIMULAZIONE

--          PUBLISHER                         BROKER                         SUBSCRIBER
-- + ------------------------- +      + ------------------ +      + ------------------------- +
-- |   + ============ +        |      |   + ---------- +   |      |         + ------------ +  |
-- |  || application ||        |      |   |  connector |   |      |         |  application |  |
-- |  + ============ +         |      |   + ---------- +   |      |         + ------------ +  |
-- |        |                  |      |         |          |      |                           |
-- |        |   + ========== + |      |    + ========== +  |      |   + ---------- +          |
-- |        +->|| connector ||-|------|-> ||   topic   ||  |      |   |  connector |          |
-- |           + ========== +  |      |   + ========== +   |      |   + ---------- +          |
-- + ------------------------- +      + ------------------ +      + ------------------------- +
-- *componenti di interesse indicate da doppie linee*


-- AREA DI INTERESSE DELLA SIMULAZIONE

-- l'area di interesse di questa simulazione comprende la funzionalità di creazione di topic e il loro aggiornamento in seguito
-- alla presenza di nuove informazioni provenienti dall'applicazione della blockchain publisher.


-- FUNZIONALITA SIMULATE

-- - Aggiornamento delle informazioni interne alla blockchain attraverso l'applicazione
-- - Creazione di topic da parte delle blockchain publisher
-- - Aggiornamento di topic da parte delle blockchain publisher


-- MODULI PERTINENTI ALLA SIMULAZIONE

-- moduli dell'architettura di interesse sono: blockchain publisher (applicazione e connettore) e topic nel broker:

-- ° Blockchain publisher:
--   - l'applicazione della blockchain lavora costantemente e aggiorna la sua informazione interna periodicamente,
--   - il connettore della blockchain crea un topic e lo aggiorna ogni volta che cambiano le informazioni dall'applicazione,

-- ° Topic:
--   - i topic del broker sono strutture che contengono informazioni provenienti dal publisher (nome, messaggio, id publisher)
--     e informazioni provenienti dai subscribers (richieste di iscrizioni) (non implementati in questa simulazione)


-- APPROSSIMAZIONI E ASTRAZIONI IN USO

-- - l'esecuzione delle applicazioni non viene definito, si semplifica l'applicazione concentrandosi sull'output che varia in maniera aleatoria

-- - l'informazione proveniente dall'applicazione viene rappresentata con un numero intero da 0 a 9

-- - la creazione dei topic da parte delle blockchain publisher viene fatta in maniera casuale, senza un movito dietro alla decisione di
--   condividere informazioni interne

-- - all'interno del topic, il nome del topic corrisponde all'id del publisher che lo ha creato.

-- - si inizia la simulazione in uno stato in cui alcune blockchain sono già iscritte come publishers e altre no, non avviene il processo
--   di iscrizione. Si da per scontato che il processo di iscrizione funzioni correttamente

-- - il sistema viene inizializzato con 1 modulo broker e 5 moduli blockchain, di cui 3 sono iscritti come publisher, e 2 non sono iscritti


-- ISTRUZIONI DI SIMULAZIONE

-- per eseguire la simulazione in NuSMV tramite il comando "simulate", è necessario aggiungere il parametro "-r", per abilitare
-- la scelta non deterministica degli stati, necessaria per la corretta rappresentazione del sistema.


-- Modulo main
MODULE main
    -- simulazione impostata con 1 broker e 5 blockchain
    VAR
        -- modulo per simulare il broker, contiene un riferimento ad ogni blockchain della rete
        broker : broker(b0, b1, b2, b3, b4);

        -- blockchain appartenenti alla rete, contengono un id e un riferimento al broker
        b0 : blockchain(0, broker);
        b1 : blockchain(1, broker);
        b2 : blockchain(2, broker);
        b3 : blockchain(3, broker);
        b4 : blockchain(4, broker);


    -- Proprietà del sistema da verificare: (gran parte delle proprietà vengono verificate sulla prima blockchain e topic, ma rappresentano il funzionamento di tutti i moduli del sistema)

    CTLSPEC -- verifica che il modulo topic non entri in deadlock (non rimanga bloccato su uno qualsiasi dei suoi stati interni di esecuzione)
        EF(!AG(broker.t0.state = creating) & !AG(broker.t0.state = updating) & !AG(broker.t0.state = accept) & !AG(broker.t0.state = decline));

    CTLSPEC -- verifica che il modulo applicazione non entri in deadlock (non rimanga bloccato su uno qualsiasi dei suoi stati interni)
        EF(!AG(b0.app.state = idle) & !AG(b0.app.state = updating));

    CTLSPEC -- verifica che il modulo connettore non entri in deadlock (non rimanga bloccato su uno quqalsiasi dei suoi stati interni)
        EF(!AG(b0.connector.state = idle) & !AG(b0.connector.state = checkIfCanPublish) & !AG(b0.connector.state = publish) & !AG(b0.connector.state = updating) & !AG(b0.connector.state = updated));

    CTLSPEC -- verifica che i publishers non pubblichino topic senza permesso (controllo da parte dei topic)
        AG(!EF(b0.connector.state = publish & broker.t0.state = decline));
    
    CTLSPEC -- verifica che i publishers non pubblichino topic senza le condizioni necessarie (controllo totale) (controllo nel caso di un publisher iscritto come tale)
        AG((b0.connector.state = checkIfCanPublish & (broker.pubs[0] = FALSE | broker.t0.name != nil | broker.t0.message != nil | broker.t0.pub != nil)) -> !EF(b0.connector.state = publish));

    CTLSPEC -- verifica che i publishers non pubblichino topic senza le condizioni necessarie (controllo totale) (controllo nel caso di un publisher non iscritto)
        AG((b2.connector.state = checkIfCanPublish & (broker.pubs[2] = FALSE | broker.t2.name != nil | broker.t2.message != nil | broker.t2.pub != nil)) -> !EF(b2.connector.state = publish));

    CTLSPEC -- verifica che un publisher con il permesso di pubblicare effettivamente pubblica
        AG((b0.connector.state = checkIfCanPublish & (broker.pubs[0] = TRUE & broker.t0.name = nil & broker.t0.message = nil & broker.t0.pub = nil)) -> AF(b0.connector.state = publish));

    CTLSPEC -- varifica che un aggiornamento di un applicazione di un publisher che ha pubblicato un topic aggiorna l'informazione nel topic
        AG((b0.app.state = updating & b0.connector.published = TRUE & broker.t0.state = idle) -> AF(broker.t0.state = updating));
        

-- Modulo per simulare il broker
-- parametri: blockchain nella rete
MODULE broker(b0, b1, b2, b3, b4)

    VAR
        -- array per tenere traccia delle blockchain iscritte come publishers
        pubs : array 0..4 of boolean;

        -- Moduli topic, il secondo parametro rappresenta la blockchain publisher che può creare un topic
        -- i topic sono mappati in modo tale che b0 può scrivere su t0 etc...
        t0 : topic(0, b0, b1, b2, b3, b4, pubs);
        t1 : topic(1, b1, b0, b2, b3, b4, pubs);
        t2 : topic(2, b2, b1, b0, b3, b4, pubs);
        t3 : topic(3, b3, b1, b2, b0, b4, pubs);
        t4 : topic(4, b4, b1, b2, b3, b0, pubs);

    ASSIGN
        -- lo stato degli array subs e pubs sono pre-impostati (mock data)
        -- sono impostati in modo tale per qui le blockchain sono:
        init(pubs[0]) := TRUE;  -- b0: publisher
        init(pubs[1]) := TRUE;  -- b1: publisher
        init(pubs[2]) := FALSE; -- b2: non publisher
        init(pubs[3]) := TRUE;  -- b3: publisher
        init(pubs[4]) := FALSE; -- b4: non publisher

        -- la lista di publishers non varia (in questa simulazione)
        next(pubs[0]) := pubs[0];
        next(pubs[1]) := pubs[1];
        next(pubs[2]) := pubs[2];
        next(pubs[3]) := pubs[3];
        next(pubs[4]) := pubs[4];


-- Modulo per simulare i topic
-- parametri: id, publisher, restanti blockchain e lista di publishers
MODULE topic( id, p, b1, b2, b3, b4, pubs)

    VAR
        -- stato per rappresentare la condizione del topic e ciò che sta svolgendo in ogni momento
        -- idle: non sta lavorando
        -- accept: accettazione di una richiesta di creazione di un topic da parte della blockchain publisher p
        -- decline: rifiuto di una richiesta di creazione di un topic da parte della blockchain publisher p
        -- creating: creazione di un nuovo topic
        -- updating: aggiornamento delle informazioni del topic
        state : {idle, accept, decline, creating, updating};
        
        -- informazioni del topic (valore nil rappresenta il caso in cui il topic non sia ancora stato creato)
        -- rappresenta il nome del topic (rappresentato con un valore da 0 a 4, corrispondente all'id del publisher)
        name : {0, 1, 2, 3, 4, nil};

        -- rappresenta il messaggio del topic (informazione proveniente dall'applicazione del publisher)
        message : {0, 1, 2, 3, 4, 5, 6, 7, 8, 9, nil};

        -- rappresenta il publisher che ha creato il topic (id del publisher)
        pub : {0, 1, 2, 3, 4, nil};

        -- normalmente il topic contiene una lista di subscriber iscritti, ma non è pertinente all campo della simulazione

    ASSIGN
        -- stato inziale: non sta lavorando
        init(state) := idle;

        -- le informazioni iniziali del topic vengono inizializzate a nil (il topic non è ancora stato creato)
        init(name) := nil;
        init(message) := nil;
        init(pub) := nil;


        next(state) :=
            case
                -- CREAZIONE:

                -- se il topic non sta lavorando, il publisher sta controllando se può pubblicare un nuovo topic e il topic già esiste
                -- o il publisher non è registrato come tale nel broker:
                -- declina la richiesta
                (state = idle) & (p.connector.state = checkIfCanPublish) & ((name != nil) | (pub != nil) | (message != nil) | (pubs[p.id] = FALSE)) : decline;
                -- se si sta declinando la richiesta del publisher e il publisher riconosce la risposta negativa:
                -- torna a non lavorare
                (state = decline) & (p.connector.state = idle) : idle; 

                -- se il topic non sta lavorando, il publisher sta controllando se può pubblicare un nuovo topic, il topic non esiste
                -- ancora e il publisher è registrato correttamente come tale nel broker:
                -- accetta la richiesta del publisher
                (state = idle) & (p.connector.state = checkIfCanPublish) & (name = nil) & (pub = nil) & (message = nil) & (pubs[p.id] = TRUE) : accept;                
                -- se la richiesta del publisher di creare un nuovo topic è stata accettata e il publisher sta pubblicando:
                -- entra in fase di creazione del topic
                (state = accept) & (p.connector.state = publish) : creating;
                
                -- se il topic sta venendo creato e le informazioni sono state aggiornate:
                -- termina il processo di creazione e torna a non lavorare
                (state = creating) & (name != nil) & (pub != nil) & (message != nil) : idle;


                -- AGGIORNAMENTO:

                -- se il topic non sta lavorando e il connettore del publisher vuole aggiornare il topic:
                -- aggiorna il topic
                (state = idle) & (p.connector.state = updating) : updating;

                -- se il topic si sta aggiornando: torna a non lavorare (richiede solo un ciclo)
                (state = updating) : idle;


                -- mantieni lo stato in ogni altro caso
                TRUE : state;
            esac;


        next(name) :=
            case
                -- se il topic sta venendo creato:
                -- prendi come nome del topic l'id del publisher
                (state = creating) : p.id;

                -- mantieni lo stato in ogni altro caso
                TRUE : name;
            esac;

        next(message) :=
            case
                -- se il topic sta venendo creato o aggiornato:
                -- prendi come messaggio l'informazione dell'applicazione del publisher
                (state = creating) | (state = updating) : p.app.info;

                -- mantieni lo stato in ogni altro caso
                TRUE : message;
            esac;

        next(pub) :=
            case
                -- se il topic sta venendo creato:
                -- prendi come informazione sul publisher l'id del publisher
                (state = creating) : p.id;

                -- mantieni lo stato in ogni altro caso
                TRUE : pub;
            esac;
        

-- Modulo per simulare le blockchain
-- parametri:     id e riferimento al broker
MODULE blockchain(id, broker)

    VAR
        -- applicazione interna alla blockchain
        app : application;
        -- connettore della blockchain, prende come parametri l'id della blockchain, il broker e l'applicazione interna
        connector : connector(id, broker, app);
        

-- Modulo per simulare l'applicazione
MODULE application

    VAR
        -- stato per rappresentare la condizione dell'applicazione
        -- idle: non sta lavorando
        -- uppdating: l'applicazione sta aggiornando la sua informazione interna
        state : {idle, updating};

        -- l'informazione interna dell'applicazione viene rappresentata con un numero intero da 0 a 9
        info : 0..9;

    ASSIGN
        -- inizialmente l'applicazione non lavora
        init(state) := idle;

        -- l'informazione iniziale è determinata casualmente
        init(info) := {0..9};


        next(state) :=
            case
                -- se l'applicazione non sta lavorando:
                -- scegli casualmente se aggiornare l'informazione interna o se non fare niente
                -- questo simula l'esecuzione di un applicazione che calcola nuovi risultati da salvare
                (state = idle) : {idle, updating};
                
                -- se l'applicazione sta aggiornando la sua informazione interna:
                -- torna a non fare niente (esegue in un ciclo)
                (state = updating) : idle;

                -- mantieni lo stato in ogni altro caso
                TRUE : state;
            esac;

        next(info) :=
            case
                -- se l'applicazione sta aggiornando:
                -- assegna un valore casuale all'informazione interna (simulazione risultati di un esecuzione)
                (state = updating) : {0..9};

                -- mantieni lo stato in ogni altro caso
                TRUE : info;
            esac;


-- Modulo per simulare il connettore delle blockchain, responsabile di creare e aggiornare topic
-- parametri: id, riferimento al broker e riferimento all'applicazione interna della blockchain
MODULE connector(id, broker, app)

    VAR
        -- stato per rappresentare la condizione del connettore
        -- idle: non sta lavorando
        -- checkIfCanPublish: controlla se può creare un topic (se non ne possiede già uno)
        -- publish: pubblica il topic con le informazioni interne della blockchain
        -- updating: aggiorna le informazioni del topic in seguito ad un cambio di informazioni interne
        -- updated: comunica che le informazioni sono state aggiornate nel topic
        state : {idle, checkIfCanPublish, publish, updating, updated};

        -- variabile per tenere traccia della possessione di un topic
        published : boolean;

    ASSIGN
        -- stato iniziale: non sta lavorando
        init(state) := idle;
        -- inizialmente il publisher non ha pubblicato nessun topic
        init(published) := FALSE;


        next(state) :=
            case
                -- PUBBLICAZIONE:
                -- se il connettore non sta lavorando e non ha mai pubblicato niente:
                -- decidi in modo casuale se controllare di essere iscritto come publisher o non fare niente
                (state = idle) & (published = FALSE) : {idle, checkIfCanPublish};

                -- se il connettore sta controllando se può pubblicare un topic e il topic corrispondente rifiuta la richiesta:
                -- torna a non lavorare
                (state = checkIfCanPublish) & (broker.t0.id = id) & (broker.t0.state = decline) : idle;
                (state = checkIfCanPublish) & (broker.t1.id = id) & (broker.t1.state = decline) : idle;
                (state = checkIfCanPublish) & (broker.t2.id = id) & (broker.t2.state = decline) : idle;
                (state = checkIfCanPublish) & (broker.t3.id = id) & (broker.t3.state = decline) : idle;
                (state = checkIfCanPublish) & (broker.t4.id = id) & (broker.t4.state = decline) : idle;

                -- se il connettore sta controllando se può pubblicare un topic e il topic corrispondente accetta la richiesta:
                -- entra in fase di pubblicazione del topic
                (state = checkIfCanPublish) & (broker.t0.id = id) & (broker.t0.state = accept) : publish;
                (state = checkIfCanPublish) & (broker.t1.id = id) & (broker.t1.state = accept) : publish;
                (state = checkIfCanPublish) & (broker.t2.id = id) & (broker.t2.state = accept) : publish;
                (state = checkIfCanPublish) & (broker.t3.id = id) & (broker.t3.state = accept) : publish;
                (state = checkIfCanPublish) & (broker.t4.id = id) & (broker.t4.state = accept) : publish;

                -- se il connettore sta pubblicando un nuovo topic e il topic è stato correttamente creato:
                -- torna a non lavorare
                -- (la correttezza del topic viene controllata attraverso la correttezza delle informazioni id e publisher)
                -- (questo controllo viene fatto per ogni id)
                (state = publish) & (broker.t0.id = id) & (broker.t0.pub = id) : idle;
                (state = publish) & (broker.t1.id = id) & (broker.t1.pub = id) : idle;
                (state = publish) & (broker.t2.id = id) & (broker.t2.pub = id) : idle;
                (state = publish) & (broker.t3.id = id) & (broker.t3.pub = id) : idle;
                (state = publish) & (broker.t4.id = id) & (broker.t4.pub = id) : idle;
                

                -- AGGIORNAMENTO:
                -- se il connettore non sta lavorando, ha già pubblicato un topic e l'applicazione si sta aggiornando:
                -- aggiorna le informazioni del topic
                (state = idle) & (published = TRUE) & (app.state = updating) : updating;

                -- se il connettore sta aggiornando il topic e il messaggio nel topic corrisponde all'informazione dell'applicazione aggiornata:
                -- indica che il topic è stato aggiornato correttamente
                -- (questo controllo viene fatto per ogni id)
                (state = updating) & (broker.t0.id = id) & (broker.t0.message = app.info) : updated;
                (state = updating) & (broker.t1.id = id) & (broker.t1.message = app.info) : updated;
                (state = updating) & (broker.t2.id = id) & (broker.t2.message = app.info) : updated;
                (state = updating) & (broker.t3.id = id) & (broker.t3.message = app.info) : updated;
                (state = updating) & (broker.t4.id = id) & (broker.t4.message = app.info) : updated;

                -- se il topic è stato aggiornato:
                -- torna a non lavorare
                -- (questo controllo viene fatto per ogni id)
                (state = updated) & (broker.t0.id = id) & (broker.t0.state = updating) : idle;
                (state = updated) & (broker.t1.id = id) & (broker.t1.state = updating) : idle;
                (state = updated) & (broker.t2.id = id) & (broker.t2.state = updating) : idle;
                (state = updated) & (broker.t3.id = id) & (broker.t3.state = updating) : idle;
                (state = updated) & (broker.t4.id = id) & (broker.t4.state = updating) : idle;


                -- mantieni lo stato in ogni altro caso
                TRUE : state;
            esac;


        next(published) :=
            case
                -- se il connettore è arrivato allo stato di pubblicazione di un nuovo topic:
                -- il connettore tiene traccia della presenza di un topic pubblicato
                (state = publish) : TRUE;

                -- mantieni lo stato in ogni altro caso
                TRUE : published;
            esac;